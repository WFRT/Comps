#SetOption('implicit_cache', 1)

############
# Settings #
############
libPath         = ['/usr/local/lib/', '/usr/local/boost/lib/', '/usr/lib/', '/usr/local/gtest-1.6.0/lib/']
cppPath         = ['/usr/local/include/', '/usr/local/boost/include/', '/usr/include/', '/usr/local/gtest-1.6.0/include/']
requiredLibs    = ['netcdf', 'netcdf_c++', 'm']
boostLibs       = ['boost_system', 'boost_filesystem']
optionalLibs    = ['grib_api', 'jasper', 'ncurses', 'gtest']
requiredHeaders = ['netcdf.hh', 'stdio.h', 'boost/shared_ptr.hpp']
cppFlags        = '-Wall -Wno-reorder -Wno-sign-compare'

###########################

def missingLib(lib, env):
   print 'ERROR: Cannot find library ' + lib + " in the search directories: [" + ', '.join(env["LIBPATH"]) + ']'
   exit()

def missingHeader(header, env):
   print 'ERROR: Cannot find header ' + header + " in the search directories: [" + ', '.join(env["CPPPATH"]) + ']'
   exit()

Import('env')
Import('exeDir')
Import('debug')

SConscript("local.scons", exports={'env': env})

libs            = requiredLibs + optionalLibs

# Set environment variables
env.Append(LIBPATH  = libPath)
env.Append(CPPFLAGS = cppFlags)
env.Append(CPPPATH  = cppPath)
env.Append(RPATH    = env["LIBPATH"])
env.Append(CXXCOMSTR  = "\033[31mCompiling $TARGET\033[0m")
env.Append(LINKCOMSTR = "\033[32mLinking $TARGET\033[0m")

########################
# Configuration checks #
########################
if not env.GetOption('clean'):
   # Check for headers
   config = Configure(env)
   for header in requiredHeaders:
      if not config.CheckCXXHeader(header):
         missingHeader(header, env)

   # Check for libraries
   for lib in requiredLibs:
      if not config.CheckLib(lib):
         missingLib(lib, env)

   # Check for boost libraries
   for lib in boostLibs:
      if not config.CheckLib(lib):
         if not config.CheckLib(lib + '-mt'):
            missingLib(lib, env)

   # Check for GRIB
   if config.CheckLib('jasper'):
      if not config.CheckLib('grib_api'):
         print "WARNING: 'grib_api' library not found. Compiling without GRIB support."
      else:
         config.env.Append(CPPDEFINES = '-DWITH_GRIB')
   else:
      print "WARNING: 'jasper' library not found. Compiling without GRIB support."

   # Check for gtest
   if config.CheckLib('gtest'):
      print "Compiling with Google test suite."
      config.env.Append(CPPDEFINES = '-DWITH_GTEST')

   # Check for NCurses
   if not config.CheckLib('ncurses'):
      print "WARNING: 'ncurses' library not found. Compiling without ncurses support."
   else:
      config.env.Append(CPPDEFINES = '-DWITH_NCURSES')
   env = config.Finish()

# Components
folders = Split('Inputs Correctors Selectors Downscalers DetMetrics Averagers Calibrators Configurations Continuous Discretes BaseDistributions Estimators Metrics Outputs ParameterIos Transforms Variables Uncertainties Smoothers Updaters Interpolators Measures Loggers Qcs Poolers Spreaders Neighbourhoods')
objs =[]
for folder in folders:
   obj = env.Object(Glob('src/' + folder + '/*.cpp'))
   objs = objs + [obj]
core           = env.Object(Glob('src/*.cpp'))

# Test targets
testTargets    = Glob('src/Tests/*.cpp')
for testTarget in testTargets:
   obj = env.Object(testTarget)
   testTarget = str(testTarget)
   # Determine the name of the test target
   slashIndex = 0
   dotIndex   = 0
   for i in range(1,len(testTarget)):
      if(testTarget[i] == '/'):
         slashIndex = i
      if(testTarget[i] == '.'):
         dotIndex = i
   testTarget = testTarget[(slashIndex+1):dotIndex]
   if(debug):
      testName = exeDir + '/tests/' + testTarget + '_debug.exe'
   else:
      testName = exeDir + '/tests/' + testTarget + '.exe'
   testObjects = [obj] + objs + [core]
   testProgram = env.Program(testName, testObjects)

compsDriver    = env.Object(Glob('src/Drivers/Comps.cpp'))
convertDriver  = env.Object(Glob('src/Drivers/Convert.cpp'))
infoDriver     = env.Object(Glob('src/Drivers/Info.cpp'))
comps          = [compsDriver] + objs + [core]
convert        = [convertDriver] + objs + [core]
info           = [infoDriver] + objs + [core]
if(debug):
   compsName   = exeDir + '/comps_debug.exe'
   convertName = exeDir + '/convert_debug.exe'
   infoName    = exeDir + '/info_debug.exe'
else:
   compsName   = exeDir + '/comps.exe'
   convertName = exeDir + '/convert.exe'
   infoName    = exeDir + '/info.exe'
compsExe   = env.Program(compsName, comps)
convertExe = env.Program(convertName, convert)
infoExe    = env.Program(infoName, info)
if(not debug):
   Default(compsExe, infoExe, convertExe)
